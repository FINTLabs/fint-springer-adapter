buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options.windowTitle = 'Fake Personnel Adapter'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}


repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://dl.bintray.com/fint/maven'
    }
}

dependencies {
    compile('org.projectlombok:lombok')
    compile('com.github.springfox.loader:springfox-loader:1.3.0')
    compile('org.glassfish.jersey.core:jersey-common:2.27')
    compile('org.glassfish.jersey.core:jersey-client:2.27')
    compile('org.jooq:jool-java-8:0.9.13')

    compile('no.fint:fint-sse:1.1.3')
    compile('no.fint:fint-event-model:3.0.0-rc-2')
    compile('no.fint:fint-relation-model:2.0.0-alpha-1')

    compile("no.fint:fint-administrasjon-resource-model-java:${apiVersion}")

    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-web')

    testCompile('cglib:cglib-nodep:3.2.6')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyExternalDependencies(type: Copy) {
    from configurations.compile
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.compile
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    baseName = 'fake-personnel-adapter'
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${apiVersion}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

sourceCompatibility = 1.8

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version, title: jar.baseName])
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}