buildscript {
    ext {
        springBootVersion = '1.5.17.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
    options.windowTitle = 'Fake Personnel Adapter'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}


repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://dl.bintray.com/fint/maven'
    }
}

dependencies {
    compile('com.google.guava:guava:27.0.1-jre')
    compile('org.projectlombok:lombok:1.18.4')
    compile('org.glassfish.jersey.core:jersey-common:2.27')
    compile('org.glassfish.jersey.core:jersey-client:2.27')
    compile('org.jooq:jool-java-8:0.9.14')
    compile('org.apache.commons:commons-lang3:3.8.1')

    compile('no.fint:fint-sse:1.2.1-rc-1')
    compile('no.fint:fint-event-model:3.0.0')
    compile('no.fint:fint-relation-model:2.0.0-alpha-1')
    compile('no.fint:fint-spring-secrets:0.1.0')

    compile("no.fint:fint-administrasjon-resource-model-java:${apiVersion}")
    compile("no.fint:fint-utdanning-resource-model-java:${apiVersion}")

    runtime('org.springframework.boot:spring-boot-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')

    testCompile('cglib:cglib-nodep:3.2.9')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${apiVersion}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

sourceCompatibility = 1.8

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version, title: jar.baseName])
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}